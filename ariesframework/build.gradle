plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.8.10'
    id "org.jlleitschuh.gradle.ktlint" version '11.4.2'
    id 'maven-publish'
}

android {
    namespace 'org.hyperledger.ariesframework'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

ktlint {
    version = "0.49.1"
    reporters {
        reporter "checkstyle"
    }
}

dependencies {
    implementation 'org.hyperledger:anoncreds_uniffi:0.2.0-wrapper.1'
    implementation 'org.hyperledger:indy_vdr_uniffi:0.2.1-wrapper.2'
    implementation 'org.hyperledger:askar_uniffi:0.2.0-wrapper.1'

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'ch.qos.logback:logback-classic:1.2.6'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.7.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-datetime:0.4.0'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'org.didcommx:peerdid:0.5.0'

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.0'
}

ext["githubUsername"] = null
ext["githubToken"] = null
ext["version"] = null

def secretPropsFile = project.rootProject.file("local.properties")
if (secretPropsFile.exists()) {
    Properties properties = new Properties()
    secretPropsFile.withInputStream { properties.load(it) }
    properties.each { k, v -> ext[k] = v }
} else {
    ext["githubUsername"] = System.getenv("GITHUB_ACTOR")
    ext["githubToken"] = System.getenv("GITHUB_TOKEN")
    ext["version"] = System.getenv("VERSION")
}

if (ext["version"] != null) {
    ext["version"] = ext["version"].replaceFirst("v", "")
} else {
    ext["version"] = "2.0.0"
}

def getExtraString(name) {
    try {
        return ext[name]
    } catch (ignored) {
        return ""
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "github"
                setUrl("https://maven.pkg.github.com/hyperledger/aries-framework-kotlin")
                credentials {
                    username = getExtraString("githubUsername")
                    password = getExtraString("githubToken")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'org.hyperledger'
                artifactId 'aries-framework-kotlin'
                version getExtraString("version")
            }
        }
    }
}